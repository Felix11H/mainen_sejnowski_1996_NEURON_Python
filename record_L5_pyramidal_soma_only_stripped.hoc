



ra        = 150.
global_ra = ra
rm        = 30000.
c_m       = 0.75
cm_myelin = 0.04
g_pas_node = 0.02

v_init    = -70.
celsius   = 37.

Ek = -90.
Ena = 60.

gna_dend = 20.
gna_node = 30000.
gna_soma = gna_dend

gkv_axon = 2000.
gkv_soma = 200.

gca = 0.3
gkm = 0.1
gkca = 3.

gca_soma = gca
gkm_soma = gkm
gkca_soma = gkca
 


create soma 

soma {
      nseg = 1
      diam = 25
      L = 35
    }



forall {
    insert pas
    Ra = ra 
    cm = c_m 
    g_pas = 1/rm
    e_pas = v_init
  }

   // na+ channels
  forall insert na


  // kv delayed rectifier channels
  soma { insert kv  gbar_kv = gkv_soma }

  // dendritic channels
  forall {
    insert km    gbar_km  = gkm
    insert kca   gbar_kca = gkca
    insert ca    gbar_ca = gca
    insert cad
  }

  soma {
    gbar_na = gna_soma
    gbar_km = gkm_soma
    gbar_kca = gkca_soma
    gbar_ca = gca_soma
  }



objref st

soma st=new IClamp(.5)
st.dur = 900
st.del = 5
st.amp = 0.2


// capture data to Vectors
objref tvec, vvec
tvec = new Vector()
vvec = new Vector()
tvec.record(&t)
vvec.record(&soma.v(0.5))

tstop = 100


proc initialize(){
  finitialize(v_init)
  fcurrent()
}

proc integrate(){
  while(t<tstop){
    fadvance()
  }
}

proc go(){
  initialize()
  integrate()
}


go()

// $o1, $o2 Vectors of x & y values, respectively
// $s3 file name string
proc xytofile() { local i  localobj tfil
  print "writing to ", $s3
  tfil = new File()
  tfil.wopen($s3)
  tfil.printf("%d\n",$o1.size())
  for i=0,$o1.size()-1 tfil.printf("%g\t%g\n", $o1.x[i], $o2.x[i])
  tfil.close()
}

xytofile(tvec, vvec, "data/L5_record_soma_only_stripped.dat")





